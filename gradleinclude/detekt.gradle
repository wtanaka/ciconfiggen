project.ext.detekt_version = "1.0.0-RC14"
repositories {
    mavenCentral()
    // For detekt
    maven { url "https://plugins.gradle.org/m2/" }
}

configurations {
    detekt
}

dependencies {
    detekt "io.gitlab.arturbosch.detekt:detekt-cli:$detekt_version"
}

// Use the command line version of detekt because
// the plugin does not deal with nested projects well
task detekt(type: JavaExec) {
    main = "io.gitlab.arturbosch.detekt.cli.Main"
    classpath = configurations.detekt
    def input = "$projectDir"
    def config = project(':gradleinclude').projectDir.path + '/detekt.yml'
    def filters = ".*test.*"
    def params = ['-i', input, '-c', config, '-f', filters]
    args(params)
}
check.dependsOn detekt